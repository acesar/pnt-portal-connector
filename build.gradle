subprojects {

  apply plugin: 'java'
  apply plugin: 'idea'

  sourceCompatibility = 1.8

  // Folder where the plugins are built
  ext.pluginsDir = "${rootProject.buildDir}/plugins"

  configurations {
    provided
    compile.extendsFrom(provided)

  }


  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    provided(fileTree(dir: "${fusionHome}/apps/connectors/connectors-rpc/libs", include: '*.jar'))
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile(group: 'org.apache.chemistry.opencmis', name: 'chemistry-opencmis-client-impl', version: '1.1.0'){
      exclude group: 'org.slf4j'
    }
    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'

  }

  jar {
    baseName = "${project.name}"
  }

  task plugin(type: Jar) {
    baseName = "${project.name}"
    manifest {
      attributes 'Plugin-Class': "${pluginClass}",
          'Plugin-Type': "connector",
          'Plugin-Id': "${pluginId}",
          'Plugin-Fusion-Version': "${fusionVersion}",
          'Plugin-Version': "${version}",
          'Plugin-Provider': "${pluginProvider}"
    }
    into('lib') {
      from configurations.compile - configurations.provided
      // Copy the plugin
      from jar
    }
    extension('zip')
  }

  task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
  }
}

task assemblePlugins(type: Copy) {
  dependsOn subprojects.assemblePlugin
}